T 56 K [установка адреса загрузки]
GK [фиксация начального адреса подпрограммы]
[0] A 3 F [код инструкции возврата в аккумулятор]
[1] T 87 @ [запись инструкции возврата]

[2] X 0 @ [отладка]
[3] X 0 @ [отладка]

[4] A 1 F [берем число разрядов на проверку >0]
[5] S 89 @ [вычитаем единицу]
[6] G 87 @ [при отрицательности окончание программы]
[7] T 90 @ [очистили аккумулятор]

[8] A 88 @ [0]
[9] T 91 @ [изменяемое число]
[10] A 89 @ [1]
[11] T 92 @ [записываем в счетчик]
[12] A 1 F [число разрядов]
[13] S 89 @ [вычитаем единицу]
[14] T 90 @ [loop 1: формируем счетчик]
[15] A 92 @ [счетчик]
[16] L 0 D [увеличиваем в два раза на каждый разряд]
[17] T 92 @ [записываем]
[18] A 77 @ [берем оператор сдвига вправо]
[19] S 3 F [вычитаем опкод]
[20] L 0 D [смещаем]
[21] A 3 F [возвращаем опкод]
[22] T 77 @ [записываем]
[23] A 63 @ [аналогично со сдвигом влево]
[24] S 2 F
[25] R 0 D
[26] A 2 F
[27] T 63 @
[28] A 90 @
[29] S 89 @ [1]
[30] E 14 @ [пока локальный счетчик положителен]
[31] T 90 @ [очистили аккумулятор]

[32] A 77 @ [берем оператор сдвига вправо]
[33] S 3 F [вычитаем опкод]
[34] R 0 D [смещаем]
[35] A 3 F [возвращаем опкод]
[36] T 77 @ [записываем]

[37] A 92 @ [счетчик]
[38] U 95 @ [заготовка для маски]
[39] S 89 @ [уменьшили счетчик на 1]
[40] T 92 @ [loop 2: формируем из числа число в коде Грея]
[41] A 91 @ [взяли текущее число]
[42] R 0 D [сдвинули на разряд вправо]
[43] T 93 @ [записали]
[44] H 91 @ [добавили изначальное число в умножающий регистр]
[45] C 93 @ [умножили логически на сдвинутое]
[46] L 0 D [умножили на два]
[47] T 94 @ [записали удвоенное логическое произведение]
[48] A 93 @ [взяли сдвинутое число]
[49] A 91 @ [прибавили изначальное]
[50] S 94 @ [вычли удвоенное логическое произведение]
[51] T 93 @ [записали в 3 ячейку]

[52] A 95 @ [взяли заготовку для маски]
[53] T 90 @ [записали в нулевую ячейку для изменений]
[54] A 1 F [взяли число разрядов]
[55] S 89 @ [уменьшили на единицу]
[56] S 89 @ [loop 3: счетчик разрядов вывода]
[57] T 96 @
[58] A 90 @ [взяли маску]
[59] R 0 D [сдвинули на разряд вправо]
[60] T 90 @ [записали маску]
[61] H 90 @ [записали маску в умножающий регистр]
[62] C 93 @ [логически умножили на выводимое число]
[63] L 512 F [сдвинули влево для вывода]
[64] L 1 F [сдвинули еще левее]
[65] T 97 @ [записали]
[66] O 97 @ [вывели разряд выводимого числа]

[67] A 64 @ [взяли оператор сдвига]
[68] S 2 F [вычли опкод]
[69] L 0 D [увеличили на разряд]
[70] A 2 F [вернули опкод]
[71] T 64 @ [перезаписали]
[72] A 96 @
[73] E 56 @ [возвращаемся для вывода следующих разрядов]

[74] T 96 @
[75] A 64 @ [взяли оператор сдвига]
[76] S 2 F [вычли опкод]
[77] R 0 D [вернули к изначальному значению]
[78] A 2 F [вернули опкод]
[79] T 64 @ [перезаписали]
[80] O 4 F [новая строка]
[81] A 91 @ [взяли изменяемое число]
[82] A 89 @ [увеличили на 1]
[83] T 91 @ [перезаписали]
[84] A 92 @ [взяли счетчик]
[85] S 89 @ [счетчик -1]
[86] E 40 @ [loop 2: выполняем, пока не кончится счетчик]
[87] E 0 F
[88] P F [0]
[89] P D [1]
[90] P F [для записи переменных]
[91] P F [для записи переменных]
[92] P F [для записи переменных]
[93] P F [для записи переменных]
[94] P F [для записи переменных]
[95] P F [для записи переменных]
[96] P F [для записи переменных]
[97] P F [для записи переменных]

GK
[0] X 14 @ [сменили регистр на цифровой]
[1] A 12 @ [переписали число разрядов в 1 ячейку]
[2] T 1 F
[3] A 15 @ [переписали опкод L во 2 ячейку]
[4] T 2 F
[5] A 16 @ [переписали опкод R в 3 ячейку]
[6] T 3 F
[7] A 13 @ [переписали оператор слвига строки]
[8] T 4 F
[9] A 7 @
[10] G 56 F
[11] Z 0 F
[12] P 2 D [число разрядов]
[13] & F [новая строка]
[14] # F [смена регистра на цифровой]
[15] L 0 F [шаблон оператора слвига влево]
[16] R 0 F [шаблон оператора сдвига вправо]
[17] EZ PF
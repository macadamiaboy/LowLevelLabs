[31] T 125 S
[32] O 38 S [сменили регистр на цифровой]
[33] E 41 S [в начало кода]
[34] P 1 L [число разрядов]
[35] P S [0]
[36] P L [1]
[37] & S [новая строка]
[38] # S [смена регистра на цифровой]
[39] L 0 S [шаблон оператора слвига влево]
[40] R 0 S [шаблон оператора сдвига вправо]
[41] A 34 S [берем число разрядов на проверку >0]
[42] S 36 S [вычитаем единицу]
[43] G 124 S [при отрицательности окончание программы]
[44] T 0 S [очистили аккумулятор]
[45] A 35 S [0]
[46] T 1 S [изменяемое число]
[47] A 36 S [1]
[48] T 2 S [записываем в счетчик]
[49] A 34 S [число разрядов]
[50] S 36 S [вычитаем единицу]
[51] T 0 S [loop 1: формируем счетчик]
[52] A 2 S [счетчик]
[53] L 0 L [увеличиваем в два раза на каждый разряд]
[54] T 2 S [записываем]
[55] A 114 S [берем оператор сдвига вправо]
[56] S 40 S [вычитаем опкод]
[57] L 0 L [смещаем]
[58] A 40 S [возвращаем опкод]
[59] T 114 S [записываем]
[60] A 100 S [аналогично со сдвигом влево]
[61] S 39 S
[6] R 0 L
[63] A 39 S
[64] T 100 S
[65] A 0 S
[66] S 36 S [1]
[67] E 51 S [пока локальный счетчик положителен]
[68] T 0 S [очистили аккумулятор]
[69] A 114 S [берем оператор сдвига вправо]
[70] S 40 S [вычитаем опкод]
[71] R 0 L [смещаем]
[72] A 40 S [возвращаем опкод]
[73] T 114 S [записываем]
[74] A 2 S [счетчик]
[75] U 5 S [заготовка для маски]
[76] S 36 S [уменьшили счетчик на 1]
[77] T 2 S [loop 2: формируем из числа число в коде Грея]
[78] A 1 S [взяли текущее число]
[79] R 0 L [сдвинули на разряд вправо]
[80] T 3 S [записали]
[81] H 1 S [добавили изначальное число в умножающий регистр]
[82] C 3 S [умножили логически на сдвинутое]
[83] L 0 L [умножили на два]
[84] T 4 S [записали удвоенное логическое произведение]
[85] A 3 S [взяли слвинутое число]
[86] A 1 S [прибавили изначальное]
[87] S 4 S [вычли удвоенное логическое произведение]
[88] T 3 S [записали в 3 ячейку]
[89] A 5 S [взяли заготовку для маски]
[90] T 0 S [записали в нулевую ячейку для изменений]
[91] A 34 S [взяли число разрядов]
[92] S 36 S [уменьшили на единицу]
[93] S 36 S [loop 3: счетчик разрядов вывода]
[94] T 6 S
[95] A 0 S [взяли маску]
[96] R 0 L [сдвинули на разряд вправо]
[97] T 0 S [записали маску]
[98] H 0 S [записали маску в умножающий регистр]
[99] C 3 S [логически умножили на выводимое число]
[100] L 512 S [сдвинули влево для вывода]
[101] L 1 S [сдвинули еще левее]
[102] T 7 S [записали]
[103] O 7 S [вывели разряд выводимого числа]
[104] A 101 S [взяли оператор сдвига]
[105] S 39 S
[106] L 0 L [увеличили на разряд]
[107] A 39 S
[108] T 101 S [перезаписали]
[109] A 6 S
[110] E 93 S [возвращаемся для вывода следующих разрядов]
[111] T 6 S
[112] A 101 S [взяли оператор сдвига]
[113] S 39 S [вычли опкод]
[114] R 0 L [вернули к изначальному значению]
[115] A 39 S [вернули опкод]
[116] T 101 S [перезаписали]
[117] O 37 S [новая строка]
[118] A 1 S [взяли изменяемое число]
[119] A 36 S [увеличили на 1]
[120] T 1 S [перезаписали]
[121] A 2 S [взяли счетчик]
[122] S 36 S [счетчик -1]
[123] E 77 S [loop 2: выполняем, пока не кончится счетчик]
[124] Z 0 S